import org.springframework.boot.gradle.plugin.SpringBootPlugin

/**
 * @author Sanguk
 */

plugins {
	id 'java'
	id 'org.springframework.boot' version '2.7.13-SNAPSHOT' apply false
	id 'io.spring.dependency-management' version '1.0.15.RELEASE' apply false
}

// Gradle's variables
ext {
	gradleDir = rootProject.file('gradle/')

	def apps = new File(rootDir, "applications").list()

	allProjects = subprojects
	javaProjects = subprojects.findAll { new File(it.projectDir, 'src/main/java').directory }
	applications = javaProjects.findAll { apps.contains(it.name) }
	libraries = javaProjects.findAll { !apps.contains(it.name) }
	apis = libraries.findAll { it.name.endsWith('-api') }
}

// Configuration to apply to all subprojects
configure(ext.get('allProjects')) {
	plugins.apply(BasePlugin)

	group = 'net.toyproject.mall'
	version = '0.0.1-SNAPSHOT'
	description = "${rootProject.description} (${group}:${name}:${version})"

	tasks.withType(AbstractCompile).each {
		it.options.encoding = 'UTF-8'
		it.sourceCompatibility = JavaVersion.VERSION_11
		it.targetCompatibility = JavaVersion.VERSION_11
	}

	// Set the maven repository
	repositories {
		mavenCentral()
		google()
		jcenter()
		maven { url 'https://repo.spring.io/milestone' }
		maven { url 'https://repo.spring.io/snapshot' }
	}
}

// Configuration to apply to the Java Projects
configure(ext.get('javaProjects')) {
	// Apply to Java
	plugins.apply(JavaPlugin)

	// Apply to Spring dependency management
	plugins.apply("org.springframework.boot")
	plugins.apply("io.spring.dependency-management")

	dependencyManagement {
		// Version management
		dependencies {
			// Springdoc
			dependency 'org.springdoc:springdoc-openapi-ui:1.7.0'

			// IO library
			dependency 'commons-io:commons-io:2.11.0'

			// Feign
			dependency 'io.github.openfeign:feign-okhttp:9.7.0'
			dependency 'io.github.openfeign:feign-jackson:9.7.0'
			dependency 'io.github.openfeign:feign-mock:9.7.0'

			// for Javascript
			dependency 'org.webjars:webjars-locator:0.40'
			dependency 'org.webjars:jquery:3.5.1'
			dependency 'org.webjars:jquery-cookie:1.4.1-1'
			dependency 'org.webjars:bootstrap:4.5.3'
			dependency 'org.webjars:vue:2.6.11'

			// JSON Web Token support for the JVM
			dependency 'io.jsonwebtoken:jjwt:0.9.1'
		}
	}

	apply from: "${gradleDir}/java.gradle"

	// Apply Lombok Annotation Processor
	apply from: "${gradleDir}/lombok.gradle"
}

// Configuration to apply to the Spring Boot Projects
configure(ext.get('applications')) {
	// Apply to War
	plugins.apply(WarPlugin)
	// Apply to SpringBoot Application
	apply from: "${gradleDir}/spring-boot.gradle"
}

// Configuration to apply to the Java Library Projects
configure(ext.get('libraries')) {
	apply from: "${gradleDir}/java-library.gradle"

	// Apply to QueryDSL
	apply from: "${gradleDir}/querydsl.gradle"
}

// Configuration to apply to the API Projects
configure(ext.get('apis')) {
	project.sourceSets.main.java.srcDir 'src/gen/java'

	apply from: "${gradleDir}/api.gradle"

}