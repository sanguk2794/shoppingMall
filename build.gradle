import org.springframework.boot.gradle.plugin.SpringBootPlugin

/**
 * @author Sanguk
 */

plugins {
	id 'java'
	id 'org.springframework.boot' version '2.7.13-SNAPSHOT' apply false
	id 'io.spring.dependency-management' version '1.0.15.RELEASE' apply false
}

// Gradle's variables
ext {
	gradleDir = rootProject.file('gradle/')

	def apps = new File(rootDir, "applications").list()

	allProjects = subprojects
	javaProjects = subprojects.findAll { new File(it.projectDir, 'src/main/java').directory }
	applications = javaProjects.findAll { apps.contains(it.name) }
	libraries = javaProjects.findAll { !apps.contains(it.name) }

	for (project in libraries) {
		println(project.name)
	}
	apis = libraries.findAll { it.name.endsWith('-api') }
}

// Configuration to apply to all subprojects
configure(ext.get('allProjects')) {
	plugins.apply(BasePlugin)

	group = 'net.toyproject.mall'
	version = '0.0.1-SNAPSHOT'
	description = "${rootProject.description} (${group}:${name}:${version})"

	tasks.withType(AbstractCompile).each {
		it.options.encoding = 'UTF-8'
		it.sourceCompatibility = JavaVersion.VERSION_11
		it.targetCompatibility = JavaVersion.VERSION_11
	}

	// Set the maven repository
	repositories {
		mavenCentral()
		google()
		jcenter()
		maven { url 'https://repo.spring.io/milestone' }
		maven { url 'https://repo.spring.io/snapshot' }
	}
}

// Configuration to apply to the Java Projects
configure(ext.get('javaProjects')) {
	// Apply to Java
	plugins.apply(JavaPlugin)

	// Apply to Spring dependency management
	plugins.apply("org.springframework.boot")
	plugins.apply("io.spring.dependency-management")

	// Apply Lombok Annotation Processor
	apply from: "${gradleDir}/lombok.gradle"
}

// Configuration to apply to the Spring Boot Projects
configure(ext.get('applications')) {
	// Apply to War
	plugins.apply(WarPlugin)
	// Apply to SpringBoot Application
	apply from: "${gradleDir}/spring-boot.gradle"
}

// Configuration to apply to the Java Library Projects
configure(ext.get('libraries')) {
	apply from: "${gradleDir}/java-library.gradle"
}

// Configuration to apply to the API Projects
configure(ext.get('apis')) {
	project.sourceSets.main.java.srcDir 'src/gen/java'

	apply from: "${gradleDir}/api.gradle"

	// Apply to QueryDSL
	// apply from: "${gradleDir}/querydsl.gradle"
}